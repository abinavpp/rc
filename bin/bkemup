#! /usr/bin/perl

use strict;
use warnings;
use Sys::Hostname;
use Getopt::Long;

sub usage {
  print
  "Usage: bkemup <options> <destination>
  -s              Dumps uncompressed size
  -h, --help      View this help\n";
}

sub die_usage {
  usage();
  exit 1;
}

sub delete_array_elements {
  my ($array_ref, @indices) = @_;

  # Remove indexes from end.
  for (sort { $b <=> $a } @indices) {
    splice(@$array_ref, $_, 1);
  }
}

my $host = hostname();

my @backup_paths = (
  "$ENV{HOME}/bkp",
  "$ENV{HOME}/doc",
  "$ENV{HOME}/pj/my",
  "$ENV{HOME}/rc",
  "$ENV{HOME}/bin",
  "$ENV{HOME}/tst/bkp",
  "$ENV{HOME}/tst/run",
  "$ENV{HOME}/tst/tkt",
  "$ENV{HOME}/www",
  "$ENV{HOME}/.config/google-chrome",
  "$ENV{HOME}/.mozilla/",
);

my @backup_globs = (
  "$ENV{HOME}/.pre-*",
  "$ENV{HOME}/.post-*"
);

my @exclude_paths = (
  "$ENV{HOME}/.config/google-chrome/Default/Service Worker/CacheStorage",
);

my $dest = ".";

GetOptions(
  "s" => \my $opt_dump_size,
  "help" => \my $opt_help)
  or die "Error parsing cmdline";

if ($opt_help) {
  usage();
  exit 0;
}

if (scalar @ARGV > 1) { die_usage(); }

if (scalar @ARGV == 1) { $dest = $ARGV[0]; }

if (! -d $dest) { die_usage(); }

for my $backup_glob (@backup_globs) {
  my @glob_files = glob($backup_glob);

  if (!@glob_files) { print "Ignoring: $backup_glob\n"; }

  for my $glob_file (@glob_files) {
    push(@backup_paths, $glob_file);
  }
}

my @ignore_indices;
for my $i (0 .. $#backup_paths) {
  if (! -e $backup_paths[$i]) {
    print "Ignoring: $backup_paths[$i]\n";
    push(@ignore_indices, $i);
  }
}

delete_array_elements(\@backup_paths, @ignore_indices);

my $backup_paths_args = join ' ', map { qq/'$_'/ } @backup_paths;
my $exclude_paths_tar = join ' ', map { '--exclude="' . $_ . '"' } @exclude_paths;

if ($opt_dump_size) {
  system("du -ch $exclude_paths_tar $backup_paths_args | sort -h | tail -n30");
  exit 0;
}

system("tar $exclude_paths_tar -czf $dest/$host.tar.gz $backup_paths_args");
